# Derive the angle from north when given one set of co-ordinates. The origin set of co-ordinates are assumed to be 0,0, so the destination will need to manipulated to work with that assumption. --coordToAngle=Category,variable,destinationX,destinationY ~ trig,coord,angle
parameters {"category":{},"variable":{},"destinationX":{"type":"number"},"destinationY":{"type":"number"}}

pi Local,pi
basicMaths Local,0.5Pi,~!Local,pi!~,*,0.5
basicMaths Local,1.5Pi,~!Local,pi!~,*,1.5

if ~!Local,destinationX!~,<,0,
	if ~!Local,destinationY!~,<,0,
		# lower left

		basicMaths Local,coordOpposite,~!Local,destinationX!~,*,-1
		basicMaths Local,coordAdjacent,~!Local,destinationY!~,*,-1

		rightTriangleGetOppositeAngle ~!Local,category!~,~!Local,variable!~,~!Local,coordOpposite!~,~!Local,coordAdjacent!~

		basicMaths ~!Local,category!~,~!Local,variable!~,~!~!Local,category!~,~!Local,variable!~!~,+,~!Local,pi!~

	elseIf ~!Local,destinationY!~,==,0,
		# middle left
		set ~!Local,category!~,~!Local,variable!~,~!Local,1.5Pi!~

	else
		# upper left
		set Local,coordOpposite,~!Local,destinationY!~
		basicMaths Local,coordAdjacent,~!Local,destinationX!~,*,-1

		rightTriangleGetOppositeAngle ~!Local,category!~,~!Local,variable!~,~!Local,coordOpposite!~,~!Local,coordAdjacent!~

		basicMaths ~!Local,category!~,~!Local,variable!~,~!~!Local,category!~,~!Local,variable!~!~,+,~!Local,1.5Pi!~



elseIf ~!Local,destinationX!~,==,0,
	if ~!Local,destinationY!~,<,0,
		# lower middle
		set ~!Local,category!~,~!Local,variable!~,~!Local,pi!~
	elseIf ~!Local,destinationY!~,==,0,
		# center
		set ~!Local,category!~,~!Local,variable!~,undefined
	else
		# upper middle
		set ~!Local,category!~,~!Local,variable!~,0

else
	if ~!Local,destinationY!~,<,0,
		# lower right
		basicMaths Local,coordOpposite,~!Local,destinationY!~,*,-1
		set Local,coordAdjacent,~!Local,destinationX!~

		rightTriangleGetOppositeAngle ~!Local,category!~,~!Local,variable!~,~!Local,coordOpposite!~,~!Local,coordAdjacent!~
		basicMaths ~!Local,category!~,~!Local,variable!~,~!~!Local,category!~,~!Local,variable!~!~,+,~!Local,0.5Pi!~

	elseIf ~!Local,destinationY!~,==,0,
		# middle right
		set ~!Local,category!~,~!Local,variable!~,~!Local,0.5Pi!~

	else
		# upper right
		set Local,coordOpposite,~!Local,destinationX!~
		set Local,coordAdjacent,~!Local,destinationY!~
		rightTriangleGetOppositeAngle ~!Local,category!~,~!Local,variable!~,~!Local,coordOpposite!~,~!Local,coordAdjacent!~

makeAvailable ~!Local,category!~,~!Local,variable!~
