# Unit tests for syntax highlighting ~ syntax,highlighting,hl,unitTest,hidden

defineTest listFeatures - Do we get expected data,
	listFeatures
	first
	
	loop
		expectNot ,~!Result,name!~,name
		expectNot ,~!Result,description!~,description
		expectNot ,~!Result,provider!~,provider
		expectNot ,~!Result,source!~,source

defineTest listFeatureSets,
	listFeatureSets
	countToVar Local,numberOfResultSets
	if ~!Local,numberOfResultSets!~,>,1,
		passTest Got featureSets.
	elseIf ~!Local,numberOfResultSets!~,==,1,
		warnTest Got one featureSet. It could be you have nothing installed yet, or it could be there is a problem.
	else
		failTest No featureSets.

defineTest registerFeatures,
	# Get a baseline
	listFeatures
	countToVar Local,sourceFeatures
	
	# Make the unitTest not interfere with the current profile
	set Local,originalProfile,~!General,profile!~
	set General,profile,syntaxHighlightingUnitTests
	clear
	
	# Run register
	registerFeatures
	clear
	retrieveResults SyntaxHighlightingFeatures,syntaxHighlightingUnitTests
	countToVar Local,actualFeatures
	
	# Test it
	expect ~!Local,sourceFeatures!~,~!Local,actualFeatures!~
	expectNot 0,~!Local,sourceFeatures!~
	expectNot 1,~!Local,sourceFeatures!~
	
	# Return profile to normal
	set General,profile,~!Local,originalProfile!~
	
	# Clean up
	unset SyntaxHighlightingFeatures,syntaxHighlightingUnitTests
	
	
