# Unit tests for IPC. ~ ipc,unitTest,hidden

defineTest IPC - prereq - procFaucet,
	testFaucets
		createProcFaucet proc,echo Hi!
		createPipe proc,.
	
	expect Hi!,~!Test,default,0!~


defineTest IPC - prereq - procFaucet inside meta faucet (obsolete),
	createMetaFaucet meta,
		createProcFaucet proc,echo Ho!
		
		createPipe .,proc
		createPipe proc,.
	
	createDumpFaucet procDump,ProcDump
	createNullFaucet null
	
	createPipe meta,procDump
	createPipe procDump,null
	
	deliverAll 10000
	
	expect Ho!,~!ProcDump,0!~
	
	deleteFaucet procDump
	deleteFaucet null
	deleteFaucet meta

defineTest IPC - prereq - procFaucet inside meta faucet,
	testFaucets
		createMetaFaucet meta,
			createProcFaucet proc,echo Ho!
			
			createPipe .,proc
			createPipe proc,.
		
		createPipe meta,.
	
	expect Ho!,~!Test,default,0!~

defineTest IPC - prereq - ping with raw json,
	getPackagePath Local,ipcPath,achel,IPC
	createProcFaucet proc,~!Local,ipcPath!~/ping.py
	createDumpFaucet procDump,Proc
	createNullFaucet procNull
	
	createPipe procDump,procNull
	createPipe proc,procDump
	
	deliver proc,,{"command":"ping","message":"blah"}
	
	deliverAll 10000
	
	unJsonify ["Proc,0","Proc,0"]
	retrieveResults Proc,0
	
	expect blah - pong,~!Proc,0,message!~
	
	deleteFaucet proc
	deleteFaucet procDump
	deleteFaucet procNull


defineTest IPC - json faucet,
	getPackagePath Local,ipcPath,achel,IPC
	createJsonIPCFaucet ipc,~!Local,ipcPath!~/ping.py
	createDumpFaucet ipcDump,IPC
	createNullFaucet ipcNull
	
	createPipe ipcDump,ipcNull
	createPipe ipc,ipcDump,message
	
	deliver ipc,command,ping
	deliver ipc,message,whay!
	
	deliverAll 10000
	
	expect whay! - pong,~!IPC,0!~
	
	deleteFaucet ipc
	deleteFaucet ipcDump
	deleteFaucet ipcNull
