# Unit tests for AchelJson. ~ hidden,unitTest,json,jsonfaucet

defineTest AchelJson - Internal - to json,
	setNested ["Local","jsonTestSrc","0",{"A":"1","B":"2","mr nul":"0"}]
	retrieveResults Local,jsonTestSrc
	
	achelJsonVarToJson Local,jsonTest
	
	# This is a hack to handel nesting json in a parameter to a feature that doesn't support it yet.
	retrieveResults Local,jsonTest
	replace ",
	replace [",","-"]
	replace ["({|})","_"]
	stashResults Local,jsonTest
	
	# Note that this expect has been mangled to match the above hack.
	expect _A:1-B:2-mr nul:0_,~!Local,jsonTest,0!~

defineTest AchelJson - Internal - to var,
	setNested ["Local","jsonTestSrc","0","{\"A\":\"1\",\"B\":\"2\",\"mr nul\":\"0\"}"]
	retrieveResults Local,jsonTestSrc
	
	achelJsonJsonToVar Local,jsonTest
	
	expect 1,~!Local,jsonTest,A!~
	expect 2,~!Local,jsonTest,B!~
	expect 0,~!Local,jsonTest,mr nul!~

defineTest AchelJson - Create faucet and send data to it (json->var),
	createJsonToVarFaucet jsonDemo,Tmp,jsonDemo
	createNullFaucet jsonNull
	createPipe jsonDemo,jsonNull
	deliver jsonDemo,default,{"A":"123"}
	deliver jsonDemo,default,{"A":"124"}
	deliverAll
	
	expect 124,~!Tmp,jsonDemo,A!~
	
	deleteFaucet jsonDemo
	deleteFaucet jsonNull
	unset Tmp,jsonDemo

defineTest AchelJson - Create shell json faucet,
	createJsonShellFaucet jsonDemo,Tmp,jsonDemo,echo '{"A":"987"}'
	createNullFaucet jsonNull
	createPipe jsonDemo,jsonNull
	
	deliverAll 100000
	
	expect 987,~!Tmp,jsonDemo,A!~

	deleteFaucet jsonDemo
	deleteFaucet jsonNull
	unset Tmp,jsonDemo

defineTest AchelJson - Create shell json faucet - 2 commands - true,
	createJsonShellFaucet jsonDemo,Tmp,jsonDemo,true;echo '{"A":"765"}'
	createNullFaucet jsonNull
	createPipe jsonDemo,jsonNull
	
	deliverAll 100000
	
	expect 765,~!Tmp,jsonDemo,A!~

	deleteFaucet jsonDemo
	deleteFaucet jsonNull
	unset Tmp,jsonDemo

defineTest AchelJson - Create shell json faucet - 2 commands - cd,
	createJsonShellFaucet jsonDemo,Tmp,jsonDemo,cd /tmp;echo '{"A":"4563"}'
	createNullFaucet jsonNull
	createPipe jsonDemo,jsonNull
	
	deliverAll 100000
	
	expect 4563,~!Tmp,jsonDemo,A!~

	deleteFaucet jsonDemo
	deleteFaucet jsonNull
	unset Tmp,jsonDemo

defineTest AchelJson - Create shell json faucet - 3 commands - stderr,
	createJsonShellFaucet jsonDemo,Tmp,jsonDemo,cd /tmp;echo '{"A":"4562"}';echo example error >&2
	createNullFaucet jsonNull
	createPipe jsonDemo,jsonNull
	
	deliverAll 100000
	
	expect 4562,~!Tmp,jsonDemo,A!~

	deleteFaucet jsonDemo
	deleteFaucet jsonNull
	unset Tmp,jsonDemo

defineTest AchelJson - Create shell json faucet - 3 commands - sh/stderr,
	createJsonShellFaucet jsonDemo,Tmp,jsonDemo,sh -c "cd /tmp;echo '{\"A\":\"4561\"}';echo example error >&2"
	createNullFaucet jsonNull
	createPipe jsonDemo,jsonNull
	
	deliverAll 100000
	
	expect 4561,~!Tmp,jsonDemo,A!~

	deleteFaucet jsonDemo
	deleteFaucet jsonNull
	unset Tmp,jsonDemo

defineTest AchelJson - Create shell json faucet - 2 commands - sh,
	createJsonShellFaucet jsonDemo,Tmp,jsonDemo,sh -c "cd /tmp;echo '{\"A\":\"4560\"}'"
	createNullFaucet jsonNull
	createPipe jsonDemo,jsonNull
	
	deliverAll 100000
	
	expect 4560,~!Tmp,jsonDemo,A!~

	deleteFaucet jsonDemo
	deleteFaucet jsonNull
	unset Tmp,jsonDemo

defineTest AchelJson - Create shell json faucet - python,
	createJsonShellFaucet jsonDemo,Tmp,jsonDemo,echo "print '{\"A\":\"102\"}'" | python
	createNullFaucet jsonNull
	createPipe jsonDemo,jsonNull
	
	deliverAll 100000
	
	expect 102,~!Tmp,jsonDemo,A!~

	deleteFaucet jsonDemo
	deleteFaucet jsonNull
	unset Tmp,jsonDemo

defineTest AchelJson - Create shell json faucet - bash,
	createJsonShellFaucet jsonDemo,Tmp,jsonDemo,echo "echo '{\"A\":\"101\"}'" | bash
	createNullFaucet jsonNull
	createPipe jsonDemo,jsonNull
	
	deliverAll 100000
	
	expect 101,~!Tmp,jsonDemo,A!~

	deleteFaucet jsonDemo
	deleteFaucet jsonNull
	unset Tmp,jsonDemo

defineTest AchelJson - jsonToChannel,
	setNested Local,example,default,{"A":"1"}
	retrieveResults Local,example
	
	achelJsonJsonToChannel
	
	stashResults Local,example
	nested
	outNow
	
	debug 0,### Test A
	expect 1,~!Local,example,A!~
	debug 0,### Test B

defineTest AchelJson - channelToJson,
	setNested Local,example,A,1
	retrieveResults Local,example
	
	achelJsonChannelToJson
	
	stashResults Local,example
	
	expect -{"default":"{\"A\":\"1\"}"},-~!Local,example!~


defineTest AchelJson - jsonToChannelFaucet,
	createJsonToChannelsFaucet jsonChannel
	createDumpFaucet jsonDump,JsonDump
	createNullFaucet jsonNull
	createPipe jsonDump,jsonNull
	createPipe jsonChannel,jsonDump,A
	
	deliver jsonChannel,default,{"A":"1"}
	
	deliverAll 100000
	
	expect 1,~!JsonDump,0!~
	
	deleteFaucet jsonChannel
	deleteFaucet jsonDump
	deleteFaucet jsonNull
	
	unsetCategory JsonDump

defineTest AchelJson - channelToJsonFaucet,
	createChannelsToJsonFaucet jsonChannel
	createDumpFaucet jsonDump,JsonDump
	createNullFaucet jsonNull
	createPipe jsonDump,jsonNull
	createPipe jsonChannel,jsonDump
	
	deliver jsonChannel,A,1
	
	deliverAll 100000
	
	expect -{"A":"1"},-~!JsonDump,0!~
	
	deleteFaucet jsonChannel
	deleteFaucet jsonDump
	deleteFaucet jsonNull
	
	unsetCategory JsonDump


defineTest AchelJson - channelToJsonFaucet to jsonToChannelFaucet,
	# This is the test that shows that everything is working. If we are able to send data into one faucet, connect it to another and recieve the correct data from the other faucet, things are goooood. :)
	
	createChannelsToJsonFaucet jsonChannelIn
	createJsonToChannelsFaucet jsonChannelOut
	createDumpFaucet jsonDump,JsonDump
	createNullFaucet jsonNull
	createPipe jsonDump,jsonNull
	createPipe jsonChannelIn,jsonChannelOut
	createPipe jsonChannelOut,jsonDump,A
	
	deliver jsonChannelIn,A,321
	
	deliverAll 100000
	
	expect 321,~!JsonDump,0!~
	
	deleteFaucet jsonChannel
	deleteFaucet jsonDump
	deleteFaucet jsonNull
	
	unsetCategory JsonDump

defineTest AchelJson - channelToJsonFaucet to jsonToChannelFaucet - lots of channels,
	# This is the test that shows that everything is working. If we are able to send data into one faucet, connect it to another and recieve the correct data from the other faucet, things are goooood. :)
	
	createChannelsToJsonFaucet jsonChannelIn
	createJsonToChannelsFaucet jsonChannelOut
	createDumpFaucet jsonDumpA,JsonDumpA
	createDumpFaucet jsonDumpB,JsonDumpB
	createDumpFaucet jsonDumpC,JsonDumpC
	createNullFaucet jsonNull
	createPipe jsonDumpA,jsonNull
	createPipe jsonDumpB,jsonNull
	createPipe jsonDumpC,jsonNull
	createPipe jsonChannelIn,jsonChannelOut
	createPipe jsonChannelOut,jsonDumpA,A
	createPipe jsonChannelOut,jsonDumpB,B
	createPipe jsonChannelOut,jsonDumpC,C
	
	deliver jsonChannelIn,A,321
	deliver jsonChannelIn,B,456
	deliver jsonChannelIn,C,987
	
	deliverAll 100000
	
	expect 321,~!JsonDumpA,0!~
	expect 456,~!JsonDumpB,0!~
	expect 987,~!JsonDumpC,0!~
	
	deleteFaucet jsonChannel
	deleteFaucet jsonDumpA
	deleteFaucet jsonDumpB
	deleteFaucet jsonDumpC
	deleteFaucet jsonNull
	
	unsetCategory JsonDump




# TODO This should probably be mored out to a new/different module
defineTest AchelDump - faucet,
	createDumpFaucet dumpDemo,DumpDemo
	createNullFaucet dumpNull
	createPipe dumpDemo,dumpNull
	deliver dumpDemo,default,hello
	
	deliverAll
	expect hello,~!DumpDemo,0!~
	
	
	deleteFaucet dumpDemo
	deleteFaucet dumpNull
