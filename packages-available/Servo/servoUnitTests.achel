# Unit tests for the Servo package. ~ unitTest,servo,hidden

defineTest Servo - getPath,
	getARBPath Local,servoPath
	expectNot ,~!Local,servoPath!~

defineTest Servo - does it start,
	getARBPath Local,servoPath
	createJsonShellFaucet servoTest,ServoTest,returnedData,~!Local,servoPath!~/servo-json.py
	createNullFaucet null
	createPipe servoTest,null
	
	deliverAll 10000
	
	expectNot ,~!ServoTest,returnedData,command!~
	
	unset ServoTest
	deleteFaucet servoTest

defineTest Servo - ping,
	getARBPath Local,servoPath
	createProcFaucet servoTest,~!Local,servoPath!~/servo-json.py
	createJsonToVarFaucet jsonFaucet,ServoTest,returnedData
	createNullFaucet null
	createPipe jsonFaucet,null
	createPipe servoTest,jsonFaucet
	
	deliverAll 10000
	
	deliver servoTest,,{"command":"ping","message":"manual ping"}
	
	deliverAll 10000
	
	expect pong,~!ServoTest,returnedData,command!~
	
	unset ServoTest
	deleteFaucet servoTest

defineTest Servo - GPIO started,
	getARBPath Local,servoPath
	createProcFaucet servoTest,~!Local,servoPath!~/servo-json.py
	createJsonToVarFaucet jsonFaucet,ServoTest,returnedData
	createNullFaucet null
	createPipe jsonFaucet,null
	createPipe servoTest,jsonFaucet
	
	deliverAll 10000
	
	deliver servoTest,,{"command":"ping","message":"none/GPIO"}
	
	deliverAll 10000
	
	if ~!ServoTest,returnedData,shortMessage!~,==,gpioStarted,
		passTest GPIO started
	else
		warnTest GPIO not started
	
	unset ServoTest
	deleteFaucet servoTest

defineTest Servo - Send some data,
	testFaucets
		getARBPath Local,servoPath
		createProcFaucet servoTest,~!Local,servoPath!~/servo-json.py
		createJsonToVarFaucet jsonFaucet,ServoTest,returnedData
		createNullFaucet null
		createPipe jsonFaucet,null
		createPipe servoTest,jsonFaucet
		
		deliver servoTest,,{"command":"nuter","message":"true"}
		
		setAllGenericServos
		
		deliver servoTest,,{"command":"setData","data":{"7":"100","11":"90"}}
		
	if ~!ServoTest,returnedData,message!~,==,Unknown command,
		failTest Not implemented
	elseIf ~!ServoTest,returnedData,message!~,==,Set 2 pins.,
		passTest Got confirmation of data.
	elseIf ~!ServoTest,returnedData,message!~,==,Failure trying to set pin 7.,
		warnTest Got an error that usually means that GPIO failed to load. If that is the case, you can ignore this test if you are simply testing that the code is working. If you are trying to understand why this isn't working on an actual device that has GPIO pins such as the raspberry Pi; then this will be a good heads up. Check that the python GPIO libraries are installed.
	else
		failTest Unknown result "~!ServoTest,returnedData,message!~"

defineTest Servo - faucet - ping,
	# This test is occasionally failing because the last faucet can only give the last result if there is a back log. If the data comes in separate iterations, the last value will be taken of each iteration. Therefore causing there to be multiple results, and choosing the first one (as done here) gives the wrong result.
	# TODO create throttleFaucet
	testFaucets
		createServoFaucet servo
		createLastFaucet last
		createPipe servo,last,message
		createPipe last,.
		
		servoPing servo,faucet
	
	expect Returned from requested ping.,~!Test,default,0!~

defineTest Servo - faucet - nuter,
	testFaucets
		createServoFaucet servo
		createLastFaucet last
		createPipe servo,last,message
		createPipe last,.
		
		servoNuter servo
	
	expect Set nutered to true(true).,~!Test,default,0!~


defineTest Servo - faucet - send live data,
	testFaucets
		createServoFaucet servo
		createLastFaucet last
		createPipe servo,last,message
		createPipe last,.
		createPipe .,servo
		
		servoNuter servo
		
		deliverAll 10000
		
		deliver servo,0,1
		deliver servo,1,2
		deliver servo,2,3
		deliver servo,test,thingn
		
		deliverAll 10000
	
	expect Set 3 pins.,~!Test,default,0!~
	
	getCategory Test
	nested
	outNow

